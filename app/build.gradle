apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    flavorDimensions "versionCode"
    defaultConfig {
        applicationId "com.samourai.sentinel"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 49
        versionName '3.6'
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "XOR_1", "\""+getApiKey("XOR_1")+"\""
            variant.buildConfigField "String", "XOR_2", "\""+getApiKey("XOR_2")+"\""
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    productFlavors {
        production {
            minSdkVersion 21
            applicationId 'com.samourai.sentinel'
            targetSdkVersion 28
            versionCode 49
            versionName '3.6'
            resValue "string", "app_name", "Sentinel"
            resValue "string", "version_name", "3.6"
        }
        staging {
            minSdkVersion 21
            applicationId 'com.samourai.sentinel.staging'
            targetSdkVersion 28
            versionCode 50
            versionName '3.7'
            resValue "string", "app_name", "Sentinel Staging"
            resValue "string", "version_name", "3.7"
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.baoyz.swipemenulistview:library:1.2.1'
    implementation 'com.neovisionaries:nv-websocket-client:1.9'
    implementation 'commons-codec:commons-codec:1.4'
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.google.guava:guava:16.0.1'
    implementation 'org.apache.commons:commons-lang3:3.3'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.55'
    implementation 'com.lambdaworks:scrypt:1.4.0'
    implementation 'net.i2p.android.ext:floatingactionbutton:1.9.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.android.support:design:28.0.0'
    implementation('com.google.zxing:core:3.3.0') {
        transitive = true
    }
    implementation 'com.yanzhenjie.zbar:camera:1.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.auth0.android:jwtdecode:1.1.1'

    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation project(path: ':tor')

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

}


def getApiKey(key){
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('samourai.api')))
    return props[key]
}

